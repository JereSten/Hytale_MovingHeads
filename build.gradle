plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id("com.gradleup.shadow") version "9.3.1"
}

group = 'de.jerst'
version = '0.1.1'

repositories {
    mavenCentral()
    maven { url "https://www.cursemaven.com" }
    flatDir {
        dirs 'libs'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

dependencies {
    // Libraries
    compileOnly name: 'HytaleServer'
    
    // Tests
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2"
    implementation "curse.maven:hyui-1431415:7588625"
}

test {
    useJUnitPlatform()
}

shadowJar {
    manifest {
        attributes["Main-Class"] = "de.jerst.plugin.movingheads.MovingHeadsPlugin"
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.runtimeClasspath]

    dependencies {
        exclude(dependency('name:HytaleServer'))
    }

    relocate 'kotlinx.coroutines', 'de.jerst.internal.coroutines'

    archiveClassifier.set('')
}

tasks.named('jar') { enabled = false }
tasks.named('assemble') { dependsOn shadowJar }

task copyToServer(type: Copy) {
    dependsOn shadowJar
    from shadowJar.archivePath
    into 'D:/Entwicklung/Hytale/Server/mods'
}

build.finalizedBy copyToServer